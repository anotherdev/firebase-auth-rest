apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'

android {
    def APP_NAME = "AuthRest"
    def APP_TITLE = "Firebase Auth REST API"

    compileSdkVersion rootProject.ext.compileSdkVersion

    defaultConfig {
        applicationId "com.anotherdev.sample.firebase.auth"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.fullVersionCode
        versionName rootProject.ext.versionName

        resValue "string", "app_name", APP_NAME
        resValue "string", "app_title", APP_TITLE

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix " - " + defaultConfig.versionCode + " DEBUG"

            resValue "string", "app_name", "Debug " + APP_NAME
            resValue "string", "app_title", "Debug " + APP_TITLE
        }
    }

    compileOptions {
        sourceCompatibility = rootProject.ext.sourceCompatibility
        targetCompatibility = rootProject.ext.targetCompatibility
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    releaseImplementation "com.github.anotherdev:firebase-auth-rest:$versionName"
    debugImplementation project(':firebase-auth-rest:core')

    implementation "androidx.appcompat:appcompat:$androidxAppCompatVersion"

    implementation 'com.facebook.android:facebook-login:6.3.0'

    implementation "com.github.akarnokd:rxjava3-bridge:$rxJavaBridgeVersion"

    implementation "com.github.florent37:inline-activity-result-rx:1.0.3"

    implementation "com.github.HITGIF:TextFieldBoxes:1.4.5"

    implementation "com.google.android.gms:play-services-auth:18.0.0"

    implementation "com.google.android.material:material:1.1.0"

    implementation platform("com.google.firebase:firebase-bom:25.2.2")
    implementation "com.google.firebase:firebase-analytics"
    implementation "com.google.firebase:firebase-database"

    //noinspection AnnotationProcessorOnCompilePath
    implementation "com.jakewharton:butterknife:10.2.1"
    annotationProcessor "com.jakewharton:butterknife-compiler:10.2.1"

    implementation "com.linecorp:linesdk:5.4.0"

    implementation "com.yarolegovich:lovely-dialog:1.1.0"

    implementation "io.reactivex.rxjava2:rxjava:2.2.19"
    implementation "io.reactivex.rxjava3:rxandroid:3.0.0"

    testImplementation "junit:junit:$junitVersion"
    androidTestImplementation "androidx.test.ext:junit:$androidxTestJunitVersion"
    androidTestImplementation "androidx.test.espresso:espresso-core:$androidxTestEspressoVersion"
}
