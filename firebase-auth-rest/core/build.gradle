apply plugin: 'com.android.library'

static def gitSha() {
    return 'git rev-parse --short HEAD'.execute().text.trim()
}

static def gitCommitHash() {
    return 'git rev-parse HEAD'.execute().text.trim()
}

static def gitBranch() {
    return 'git rev-parse --abbrev-ref HEAD'.execute().text.trim()
}

static def buildTime() {
    return (new Date().getTime() / 1000).toInteger()
}

static def wrapString(aString) {
    return '"' + aString + '"'
}

static def wrapBoolean(value) {
    return "Boolean.parseBoolean(" + wrapString(value) + ")"
}

static def wrapInteger(value) {
    return "" + value + ""
}

android {
    def GIT_SHA = "GIT_SHA"
    def GIT_COMMIT_HASH = "GIT_COMMIT_HASH"
    def GIT_BRANCH = "GIT_BRANCH"

    compileSdkVersion rootProject.ext.compileSdkVersion

    defaultConfig {
        buildConfigField 'String', GIT_SHA, wrapString(gitSha())
        buildConfigField 'String', GIT_COMMIT_HASH, wrapString(gitCommitHash())
        buildConfigField 'String', GIT_BRANCH, wrapString(gitBranch())

        minSdkVersion rootProject.ext.minSdkVersionFirebaseAuthRest
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.fullVersionCode
        versionName rootProject.ext.versionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility = rootProject.ext.sourceCompatibility
        targetCompatibility = rootProject.ext.targetCompatibility
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'com.f2prateek.rx.preferences2:rx-preferences:2.0.0'

    implementation "com.github.akarnokd:rxjava3-bridge:3.0.0"
    implementation "com.github.akarnokd:rxjava3-retrofit-adapter:3.0.0"

    api "com.google.code.gson:gson:$gsonVersion"

    implementation platform("com.google.firebase:firebase-bom:25.2.2")
    implementation "com.google.firebase:firebase-auth-interop:19.0.0"
    api "com.google.firebase:firebase-common"
    implementation "com.google.firebase:firebase-core"

    implementation "com.jakewharton.timber:timber:$timberVersion"

    implementation "com.squareup.okhttp3:logging-interceptor:$okhttpVersion"
    implementation "com.squareup.okhttp3:okhttp:$okhttpVersion"

    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"

    api "io.reactivex.rxjava3:rxjava:$rxJava3Version"

    compileOnly "org.immutables:gson:$immutablesVersion"
    annotationProcessor "org.immutables:value:$immutablesVersion"
    //noinspection AnnotationProcessorOnCompilePath
    compileOnly "org.immutables:value:$immutablesVersion"

    testImplementation "junit:junit:$junitVersion"
    androidTestImplementation "androidx.test.ext:junit:$androidxTestJunitVersion"
    androidTestImplementation "androidx.test.espresso:espresso-core:$androidxTestEspressoVersion"
}
